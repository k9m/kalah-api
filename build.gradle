plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'org.openapi.generator' version '4.2.2'
	id 'java'
	id 'jacoco'
}

ext {
	cucumberVersion = '5.1.0'
	swaggerVersion = '2.9.2'
}

apply plugin: 'org.openapi.generator'
apply plugin: 'jacoco'

group = 'org.k9m.kalah'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	implementation("io.springfox:springfox-swagger2:$swaggerVersion")
	implementation("io.springfox:springfox-swagger-ui:$swaggerVersion")
	implementation("org.openapitools:jackson-databind-nullable:0.2.1")

	compileOnly 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testCompile group: "de.flapdoodle.embed", name: "de.flapdoodle.embed.mongo"
	

	testImplementation group : 'io.cucumber', name : 'cucumber-java', version: cucumberVersion
	testImplementation group : 'io.cucumber', name : 'cucumber-java', version: cucumberVersion
	testImplementation group : 'io.cucumber', name : 'cucumber-junit', version: cucumberVersion
	testImplementation group : 'io.cucumber', name : 'cucumber-spring', version: cucumberVersion
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/contract.yml"
	outputDir = "$buildDir/generated-sources"
	apiPackage = "org.k9m.kalah.api"
	modelPackage = "org.k9m.kalah.api.model"
	configOptions = [
			dateLibrary: "java8",
			interfaceOnly: "true",
			swaggerDocketConfig: "true",
			configPackage: "org.k9m.kalah.api.config",
			java8: "false"
	]
	systemProperties = [
			modelDocs: "false"
	]
	logToStderr = true
}

test {
	useJUnit()
	finalizedBy jacocoTestReport

	jacoco {
		destinationFile = file("$buildDir/jacoco/jacoco.exec")
		classDumpDir = file("$buildDir/jacoco/classpathdumps")
	}

	jacocoTestReport {
		reports {
			xml.enabled true
			csv.enabled true
			html.enabled true
			html.destination file("${buildDir}/jacoco-reports")
		}
	}
}

sourceSets {
	main {
		java {
			srcDir 'build/generated-sources/src/main/java'
		}
	}
}

springBoot {
	buildInfo {
		properties {
			artifact = project.name
			version = rootProject.version
			group = project.group
			name = project.description
		}
	}
}

compileJava.dependsOn tasks.openApiGenerate
